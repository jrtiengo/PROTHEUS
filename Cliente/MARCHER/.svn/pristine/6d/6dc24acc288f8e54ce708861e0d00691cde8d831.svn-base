#INCLUDE "PROTHEUS.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "TOPCONN.CH"
/*
{Protheus.doc} MARA060
Programa que far· a atualizaÁ„o das tabelas na Mercos via WebService
@type User function
@author Jean Rehermann - Solutio
@since 15/11/2021
*/
User Function MARA060()

	Private lBlind   := IsBlind()
	Private bProcInc := {|| Iif( !lBlind, IncProc(), 0 ) }
	Private aLogMerc := {}

	If lBlind
		EnvMercos() // Criar o ambiente de execuÁ„o
	EndIf

	// Atualizar os produtos
	//Processa({|| ApiProdMerc() }, "Integrando produtos...")

	// Atualizar os clientes
	//Processa({|| ApiCliMerc() }, "Integrando clientes...")

	// Atualizar as condiÁıes de pagamento
	Processa({|| ApiPagMerc() }, "Integrando condiÁ„o de pagamento...")

	If lBlind
		RpcClearEnv()
	EndIf

Return

/*
Cria o ambiente para a execuÁ„o do processo no schedule
*/
Static Function EnvMercos()

	Local cRpcEmp		:= "01"	     // Empresa
	Local cRpcFil		:= "01"	     // Filial Principal
	Local cEnvMod		:= "FAT"     // Modulo
	Local cFunName		:= "MARA060" // Funcao
    Local aTables 		:= {"SB1","SA1"}

	RPCSetType(3) //Define que o ambiente nao consome licenca
	RpcSetEnv( cRpcEmp , cRpcFil , , , cEnvMod, cFunName , aTables, , , ,  )

Return Nil

/*
Efetua a atualizaÁ„o dos produtos no site da Mercos
*/
Static Function ApiProdMerc()

	Local cSql      := ""
	Local cJson     := ""
	Local cDescProd := ""
	Local cMetodo   := ""
	Local nPos      := 0
	Local nTotal    := 0
	Local aRet      := {}
	Local nSleep    := SuperGetMv("ES_DLYWSME", .F., 250) // Milissegundos de delay entre as requisiÁıes ao webservice
	Local cAliasSB1 := GetNextAlias()

	// https://mercos.docs.apiary.io/#reference/0/produtos/incluir-um-produto?console=1

	cSql := "SELECT DA1_PRCVEN, B1_COD, B1_DESC, B1_IDMERCO, B1_POSIPI, "
	CsQL += " B1_UM, B1_IPI, B1_PESO, SB1.R_E_C_N_O_ RECNO "
	cSql += " FROM "+ RetSqlName("DA1") +" DA1, "+ RetSqlName("SB1") +" SB1 "
	cSql += " WHERE DA1_CODPRO = B1_COD "
	cSql += " AND DA1_CODTAB = '504' "
	cSql += " AND B1_DTMERCO = ' ' "
	cSql += " AND DA1.D_E_L_E_T_ = ' ' "
	cSql += " AND SB1.D_E_L_E_T_ = ' ' "

	dbUseArea( .T., "TOPCONN", TCGenQry( NIL, NIL, cSql ), cAliasSB1, .F., .T. )
	//TCQuery cSql New Alias cAliasSB1

	If !lBlind
		Count To nTotal
		ProcRegua( nTotal )
	EndIf

	(cAliasSB1)->( dbGoTop() )

	While !(cAliasSB1)->( Eof() )

		cDescProd := fRemCar( (cAliasSB1)->B1_DESC )

		cJson := '{'
		cJson += '"codigo": "'+ AllTrim( (cAliasSB1)->B1_COD ) +'",'
		cJson += '"nome": "'+ cDescProd +'",'
		cJson += '"preco_tabela": '+ IIf( (cAliasSB1)->DA1_PRCVEN > 0, cValToChar( (cAliasSB1)->DA1_PRCVEN ), 'null' ) +','
		cJson += '"comissao": '+ IIf( (cAliasSB1)->B1_COMIS1 > 0, cValToChar( (cAliasSB1)->B1_COMIS1 ), 'null' ) +','
		cJson += '"ipi": '+ IIf( (cAliasSB1)->B1_IPI > 0, cValToChar( (cAliasSB1)->B1_IPI ), 'null' ) +','
		cJson += '"tipo_ipi": "P",'
		cJson += '"moeda": "0",'
		cJson += '"unidade": "'+ AllTrim( (cAliasSB1)->B1_UM ) +'",'
		cJson += '"peso_bruto": '+ cValToChar( (cAliasSB1)->B1_PESO ) +','
		cJson += '"codigo_ncm": "'+ AllTrim( (cAliasSB1)->B1_POSIPI ) +'"'
		cJson += '}'

		// Verifica se o produto j· existe no Mercos, para fazer um PUT ao invÈs de um POST
		cMetodo := Iif( Empty( (cAliasSB1)->B1_IDMERCO ), "POST", "PUT" )

		// Faz a requisiÁ„o no webservice
		aRet := U_SndJSON( cMetodo, "/api/v1/produtos", cJson )
		Sleep( nSleep )

		If aRet[ 1 ] == "201" .And. Len( aRet[ 2 ] ) > 0

			nPos := aScan( aRet[2], {|o| Upper( o[ 1 ] ) == "MEUSPEDIDOSID"} )

			If nPos > 0

				SB1->( dbGoTo( (cAliasSB1)->RECNO ) )
				SB1->( RecLock("SB1", .F.) )
				SB1->B1_IDMERCO := Val( aRet[2, nPos, 2 ] )
				SB1->B1_DTMERCO := cValToChar( Year( Date() ) ) +'-'+ StrZero( Month( Date() ), 2 ) +'-'+ StrZero( Day( Date() ), 2 ) +' '+ Time()
				SB1->( MsUnLock() )

			EndIf

		EndIf

		Eval( bProcInc )

		(cAliasSB1)->( dbSkip() )

	End

	(cAliasSB1)->( dbCloseArea() )

Return

/*
Efetua a atualizaÁ„o dos clientes no site da Mercos
*/
Static Function ApiCliMerc()

	Local cSql      := ""
	Local cMetodo   := ""
	Local aRet      := {}
	Local aEndStr   := {}
	Local nPos      := 0
	Local nTotal    := 0
	Local nSleep    := SuperGetMv("ES_DLYWSME", .F., 250) // Milissegundos de delay entre as requisiÁıes ao webservice
	Local cAliasSA1 := GetNextAlias()

	cSql := "SELECT A1_NOME, A1_COD, A1_END, A1_EST, A1_NOME, A1_NREDUZ, A1_PESSOA,"
	cSql += " A1_CGC, A1_INSCR, A1_CEP, A1_BAIRRO, A1_MUN, A1_EMAIL, A1_TEL, A1_DDD, A1_FAX,"
	cSql += " A1_IDMERCO, A1_DTMERCO, R_E_C_N_O_ RECNO "
	cSql += " FROM "+ RetSqlName("SA1")
	cSql += " WHERE D_E_L_E_T_ = ' ' "
	cSql += "   AND A1_MSBLQL <> '1' "
	cSql += "   AND A1_DTMERCO = ' ' "

	dbUseArea( .T., "TOPCONN", TCGenQry( NIL, NIL, cSql ), cAliasSA1, .F., .T. )
	//TCQuery cSql New Alias cAliasSA1

	If !lBlind
		Count To nTotal
		ProcRegua( nTotal )
	EndIf

	(cAliasSA1)->( dbGoTop() )

	While !(cAliasSA1)->( Eof() )

		// Obtem a estrutura do endereÁo [ RUA, nNUMERO, cNUMERO, COMPLEMENTO ]
		aEndStr := FisGetEnd( (cAliasSA1)->A1_END, (cAliasSA1)->A1_EST )

		cJson := '{'
		cJson += '"razao_social": "'+  AllTrim( fRemCar( (cAliasSA1)->A1_NOME ) ) +'",'
		cJson += '"nome_fantasia": "'+ AllTrim( fRemCar( (cAliasSA1)->A1_NREDUZ ) ) +'",'
		cJson += '"tipo": "'+          (cAliasSA1)->A1_PESSOA +'",'
		cJson += '"cnpj": "'+          (cAliasSA1)->A1_CGC +'",'

		If !Empty( (cAliasSA1)->A1_INSCR )
			cJson += '"inscricao_estadual": "'+ AllTrim( (cAliasSA1)->A1_INSCR ) +'",'
		EndIf

		cJson += '"rua": "'+ AllTrim( fRemCar( aEndStr[ 1 ] ) ) +'",'
		cJson += '"numero": "'+ Iif( !Empty( aEndStr[ 3 ]), fRemCar( AllTrim( aEndStr[ 3 ] ) ), "SN" ) +'",'

		If !Empty( aEndStr[ 4 ] )
			cJson += '"complemento": "'+ AllTrim( fRemCar( aEndStr[ 4 ] ) ) +'",'
		EndIf

		cJson += '"cep": "'+    (cAliasSA1)->A1_CEP +'",'
		cJson += '"bairro": "'+ AllTrim( fRemCar( (cAliasSA1)->A1_BAIRRO ) ) +'",'
		cJson += '"cidade": "'+ AllTrim( (cAliasSA1)->A1_MUN ) +'",'
		cJson += '"estado": "'+ (cAliasSA1)->A1_EST +'",'

		If !Empty( (cAliasSA1)->A1_EMAIL ) .And. Len( AllTrim( (cAliasSA1)->A1_EMAIL ) ) > 10 .And. At( "@", (cAliasSA1)->A1_EMAIL ) > 0
			cJson += '"emails": [{"email": "'+ AllTrim( (cAliasSA1)->A1_EMAIL ) +'"}],'
		EndIf

		If !Empty( (cAliasSA1)->A1_TEL )
			cJson += '"telefones": ['
			cJson += '{"numero": "('+ AllTrim((cAliasSA1)->A1_DDD) +')'+ AllTrim((cAliasSA1)->A1_TEL) +'"}'
			If !Empty( (cAliasSA1)->A1_FAX )
				cJson += ','
				cJson += '{"numero": "('+ AllTrim((cAliasSA1)->A1_DDD) +')'+ AllTrim((cAliasSA1)->A1_FAX) +'"}'
			EndIf
			cJson += '],'
		EndIf
		cJson += '"excluido": false'
		cJson += '}'

		// Verifica se o cliente j· existe no Mercos, para fazer um PUT ao invÈs de um POST
		cMetodo := Iif( Empty( (cAliasSA1)->A1_IDMERCO ), "POST", "PUT" )

		cJson := '{"razao_social": "HENRICUS JOHANNES  M. A.","nome_fantasia": "HENRICUS JOHANNES","tipo": "F","cnpj": "22016945087   ","inscricao_estadual": "0891118101","rua": "ESQUINA BECK - GRANJA ZEELANDIA","numero": "SN","cep": "98300000","bairro": "ESQUINA BECK","cidade": "PALMEIRA DAS MI","estado": "RS","emails": [{"email": "compras@granjaguara.com.br"}],"telefones": [{"numero": "(55)33753081"}],"excluido": false}'

		aRet := U_SndJSON( cMetodo, "/api/v1/clientes", cJson )
		Sleep( nSleep )

		If aRet[ 1 ] == "201" .And. Len( aRet[ 2 ] ) > 0

			nPos := aScan( aRet[2], {|o| Upper( o[ 1 ] ) == "MEUSPEDIDOSID"} )

			If nPos > 0

				SA1->( dbGoTo( (cAliasSA1)->RECNO ) )
				SA1->( RecLock("SA1", .F.) )
				SA1->A1_IDMERCO := Val( aRet[2, nPos, 2 ] )
				SA1->A1_DTMERCO := cValToChar( Year( Date() ) ) +'-'+ StrZero( Month( Date() ), 2 ) +'-'+ StrZero( Day( Date() ), 2 ) +' '+ Time()
				SA1->( MsUnLock() )

			EndIf

		EndIf

		Eval( bProcInc )

		(cAliasSA1)->( dbSkip() )

	End

	(cAliasSA1)->( dbCloseArea() )

Return

/*
Efetua a atualizaÁ„o das condiÁıes de pagamento no site da Mercos
*/
Static Function ApiPagMerc()

	Local cSql      := ""
	Local cJson     := ""
	Local cDesc     := ""
	Local nPos      := 0
	Local aRet      := {}
	Local nSleep    := SuperGetMv("ES_DLYWSME", .F., 250) // Milissegundos de delay entre as requisiÁıes ao webservice
	Local cCondPag  := SuperGetMv("ES_CNPGMER", .F., "'508','509','510','511','512','513','514','999'") // CondiÁıes de pagamento a serem integradas
	Local cAliasSE4 := GetNextAlias()

	// https://mercos.docs.apiary.io/#reference/0/produtos/incluir-um-produto?console=1

	cSql := "SELECT E4_CODIGO, REPLACE( REPLACE(E4_DESCRI,'/',','), '>', '' ) E4_DESCRI, R_E_C_N_O_ RECNO "
	cSql += " FROM "+ RetSqlName("SE4")
	cSql += " WHERE D_E_L_E_T_ = ' ' "
	cSql += "   AND E4_CODIGO IN ("+ cCondPag +") "
	cSql += "   AND E4_DTMERCO = ' ' "
	cSql += " ORDER BY E4_DESCRI "

	dbUseArea( .T., "TOPCONN", TCGenQry( NIL, NIL, cSql ), cAliasSE4, .F., .T. )

	While !(cAliasSE4)->( Eof() )

		cDesc := fRemCar( (cAliasSE4)->E4_DESCRI )

		cJson := '{'
		cJson += '"nome": "'+ (cAliasSE4)->E4_CODIGO +'-'+ cDesc +'"'
		cJson += '}'

		// Faz a requisiÁ„o no webservice
		aRet := U_SndJSON( "POST", "/api/v1/condicoes_pagamento", cJson )
		Sleep( nSleep )

		If aRet[ 1 ] == "201" .And. Len( aRet[ 2 ] ) > 0

			nPos := aScan( aRet[2], {|o| Upper( o[ 1 ] ) == "MEUSPEDIDOSID"} )

			If nPos > 0

				SE4->( dbGoTo( (cAliasSE4)->RECNO ) )
				SE4->( RecLock("SE4", .F.) )
				SE4->E4_IDMERCO := Val( aRet[2, nPos, 2 ] )
				SE4->E4_DTMERCO := cValToChar( Year( Date() ) ) +'-'+ StrZero( Month( Date() ), 2 ) +'-'+ StrZero( Day( Date() ), 2 ) +' '+ Time()
				SE4->( MsUnLock() )

			EndIf

		EndIf

		(cAliasSE4)->( dbSkip() )

	End

	(cAliasSE4)->( dbCloseArea() )

Return


/*
Faz o envio do Json para o webservice e resebe o retorno
*/
User Function SndJSON( cMetodo, cUri, cJson )

	Local oResult
	Local lOk       := .T.
	Local nLoop     := 0
	Local cResult   := ""
	Local cHttpCode := ""
	Local cJsonTxt  := ""
	Local cMsgErr   := "IntegraÁ„o Mercos / Marcher - "
	Local aHeader   := {}
	Local aResult   := {}
	Local cUrl      := SuperGetMv("ES_MURLAPI", .F., "") // URL da API da Mercos para WebServices
	Local cTokenApp := SuperGetMv("ES_MAPPTOK", .F., "") // ApplicationToken ñ Em Base64
	Local cTokenCpn := SuperGetMv("ES_MCPNTOK", .F., "") // CompanyToken ñ Em Base64

	Private oRest
	Private oJson

	DEFAULT cMetodo := ""
	DEFAULT cUri    := ""
	DEFAULT cJson   := ""

	// ValidaÁıes dos par‚metros
	If Empty( cUrl )
		cMsgErr += "URL n„o informada, par‚metro ES_MURLAPI"
		lOk := .F.
	EndIf

	If lOk .And. Empty( cMetodo )
		cMsgErr += "MÈtodo da requisiÁ„o n„o passado para a funÁ„o (PUT/POST/GET)"
		lOk := .F.
	EndIf

	If lOk .And. cMetodo $ "PUT/POST" .And. Empty( cJson )
		cMsgErr += "JSON n„o passado para a funÁ„o quando POST ou PUT"
		lOk := .F.
	EndIf

	If lOk .And. Empty( cTokenApp )
		cMsgErr += "ApplicationToken n„o informado, par‚metro ES_MAPPTOK"
		lOk := .F.
	Else
		// Decodifica o ApplicationToken
		cTokenApp := Decode64( cTokenApp )
	EndIf

	If lOk .And. Empty( cTokenCpn )
		cMsgErr += "CompanyToken n„o informado, par‚metro ES_MCPNTOK"
		lOk := .F.
	Else
		// Decodifica o CompanyToken
		cTokenCpn := Decode64( cTokenCpn )
	EndIf

	If lOk

		// Cria o objeto Rest que far· a interaÁ„o com o webservice
		oRest := FWRest():New( cUrl )

		// Cria o objeto Json para receber o texto e validar as sintaxes
		oJson := JsonObject():New()

		If cMetodo $ "PUT/POST"
			// Efetua a validaÁ„o do Json que foi passado
			cResult := oJson:FromJson( cJson )

			// Verifico erros
			If ValType( cResult ) <> "U"
				cMsgErr += "Erro na validaÁ„o do Json: "+ cResult
				lOk := .F.
			Else
				cJsonTxt := cJson
			EndIf
		EndIf

	EndIf

	If lOk

		// Setar o caminho da requisiÁ„o
		oRest:setPath( cUri )

		// Definir os cabeÁalhos
		aAdd( aHeader, "Content-Type:application/json" )
		aAdd( aHeader, "ApplicationToken:"+ cTokenApp )
		aAdd( aHeader, "CompanyToken:"+ cTokenCpn )

		// Definir o mÈtodo: POST (inclus„o), GET (requisiÁ„o), PUT (alteraÁ„o)
		If cMetodo == "POST"

			oRest:SetPostParams( cJsonTxt )  // Seto o JSON para o POST
			lOk := oRest:Post( aHeader )     // Executo o POST no serviÁo
			cHttpCode := oRest:GetHTTPCode() // Verifica o cÛdigo de retorno da requisiÁ„o HTTP

			If !lOk
				cMsgErr += "Erro no POST : "+ DecodeUTF8( oRest:getLastError() ) // Quando d· certo retorna '201 Created'
				cResult := AllTrim( DecodeUTF8( oRest:getResult() ) ) // Mensagens de erro

				If At( '"erros":', cResult ) > 0
					oJson:FromJson( cResult )
					If aScan( oJson:GetNames(), { |o| o == "erros" } ) > 0
						aResult := oJson:GetJSonObject('erros')
						If Len( aResult ) > 0
							For nLoop := 1 To Len( aResult )
								cMsgErr +=  CHR(13) + CHR(10) +"Campo: "+ aResult[nLoop]["campo"] + CHR(13) + CHR(10)
								cMsgErr += "Mensagem: "+ aResult[nLoop]["mensagem"]
							Next
						EndIf
					EndIf
					aResult := {}
					cMsgErr := OemToAnsi( cMsgErr )
					cResult := cMsgErr
				EndIf
			Else
				// Obtem o Response da requisiÁ„o realizada
				oResult := Iif( ValType( oRest:oResponseH ) == "O", oRest:oResponseH, Nil )
				aResult := Iif( oResult != Nil, oRest:oResponseH:aHeaderFields, {} )
				cResult := AllTrim( DecodeUTF8( oRest:getResult() ) )
			EndIf

		Elseif cMetodo == "GET"

			lOk := oRest:Get( aHeader )     // Executo o GET no serviÁo
			cHttpCode := oRest:GetHTTPCode() // Verifica o cÛdigo de retorno da requisiÁ„o HTTP

			If !lOk
				cMsgErr += "Erro no GET : "+ DecodeUTF8( oRest:getLastError() ) // Quando d· certo retorna '201 Created'
				cResult := AllTrim( DecodeUTF8( oRest:getResult() ) ) // Mensagens de erro

				If At( '"erros":', cResult ) > 0
					oJson:FromJson( cResult )
					If aScan( oJson:GetNames(), { |o| o == "erros" } ) > 0
						aResult := oJson:GetJSonObject('erros')
						If Len( aResult ) > 0
							For nLoop := 1 To Len( aResult )
								cMsgErr +=  CHR(13) + CHR(10) +"Campo: "+ aResult[nLoop]["campo"] + CHR(13) + CHR(10)
								cMsgErr += "Mensagem: "+ aResult[nLoop]["mensagem"]
							Next
						EndIf
					EndIf
					aResult := {}
					cMsgErr := OemToAnsi( cMsgErr )
					cResult := cMsgErr
				EndIf
			Else
				// Obtem o Response da requisiÁ„o realizada
				oResult := Iif( ValType( oRest:oResponseH ) == "O", oRest:oResponseH, Nil )
				aResult := Iif( oResult != Nil, oRest:oResponseH:aHeaderFields, {} )
				cResult := AllTrim( DecodeUTF8( oRest:getResult() ) )
			EndIf

		ElseIf cMetodo == "PUT"

		Else
			cMsgErr := "O mÈtodo enviado n„o È suportado: "+ cMetodo
			lOk := .F.
		EndIf

	EndIf

	// Em caso de erro exibe a mensagem, no console ou popup
	If !lOk
		If lBlind
			ConOut( cMsgErr )
		Else
			ConOut( cMsgErr )
		EndIf
	Else
		ConOut( cResult )
	EndIf

	FreeObj( oRest )
	FreeObj( oJson )

Return( { cHttpCode, aResult, cResult } )

// FunÁ„o que faz a eliminaÁ„o de caracteres especiais de uma string
Static Function fRemCar( cTexto )

	Local cTextOk := cTexto

	cTextOk := AllTrim( FwNoAccent( cTextOk ) )
	cTextOk := StrTran( cTextOk, "≥", "" )
	cTextOk := StrTran( cTextOk, "≤", "" )
	cTextOk := StrTran( cTextOk, "∫", "" )
	cTextOk := StrTran( cTextOk, "™", "" )
	cTextOk := StrTran( cTextOk, "$", "" )
	cTextOk := StrTran( cTextOk, "%", "" )
	cTextOk := StrTran( cTextOk, "&", "" )
	cTextOk := StrTran( cTextOk, "'", "" )
	cTextOk := StrTran( cTextOk, "~", "" )
	cTextOk := StrTran( cTextOk, "/", "" )
	cTextOk := StrTran( cTextOk, "\", "" )
	cTextOk := StrTran( cTextOk, "#", "" )
	cTextOk := StrTran( cTextOk, "*", "" )
	cTextOk := StrTran( cTextOk, "+", "" )
	cTextOk := StrTran( cTextOk, '"', '' )
	cTextOk := StrTran( cTextOk, "ù", "" )
	cTextOk := StrTran( cTextOk, "·", "" )
	cTextOk := StrTran( cTextOk, "¡", "" )
	cTextOk := StrTran( cTextOk, "‡", "" )
	cTextOk := StrTran( cTextOk, "¿", "" )
	cTextOk := StrTran( cTextOk, "‚", "" )
	cTextOk := StrTran( cTextOk, "¬", "" )
	cTextOk := StrTran( cTextOk, "„", "" )
	cTextOk := StrTran( cTextOk, "√", "" )
	cTextOk := StrTran( cTextOk, "‰", "" )
	cTextOk := StrTran( cTextOk, "ƒ", "" )
	cTextOk := StrTran( cTextOk, "È", "" )
	cTextOk := StrTran( cTextOk, "…", "" )
	cTextOk := StrTran( cTextOk, "Ë", "" )
	cTextOk := StrTran( cTextOk, "»", "" )
	cTextOk := StrTran( cTextOk, "Í", "" )
	cTextOk := StrTran( cTextOk, " ", "" )
	cTextOk := StrTran( cTextOk, "Î", "" )
	cTextOk := StrTran( cTextOk, "À", "" )
	cTextOk := StrTran( cTextOk, "Ì", "" )
	cTextOk := StrTran( cTextOk, "Õ", "" )
	cTextOk := StrTran( cTextOk, "Ï", "" )
	cTextOk := StrTran( cTextOk, "Ã", "" )
	cTextOk := StrTran( cTextOk, "Ô", "" )
	cTextOk := StrTran( cTextOk, "œ", "" )
	cTextOk := StrTran( cTextOk, "Û", "" )
	cTextOk := StrTran( cTextOk, "Ú", "" )
	cTextOk := StrTran( cTextOk, "”", "" )
	cTextOk := StrTran( cTextOk, "“", "" )
	cTextOk := StrTran( cTextOk, "ı", "" )
	cTextOk := StrTran( cTextOk, "’", "" )
	cTextOk := StrTran( cTextOk, "Ù", "" )
	cTextOk := StrTran( cTextOk, "‘", "" )
	cTextOk := StrTran( cTextOk, "ˆ", "" )
	cTextOk := StrTran( cTextOk, "÷", "" )
	cTextOk := StrTran( cTextOk, "˙", "" )
	cTextOk := StrTran( cTextOk, "˘", "" )
	cTextOk := StrTran( cTextOk, "⁄", "" )
	cTextOk := StrTran( cTextOk, "Ÿ", "" )
	cTextOk := StrTran( cTextOk, "˚", "" )
	cTextOk := StrTran( cTextOk, "€", "" )
	cTextOk := StrTran( cTextOk, "¸", "" )
	cTextOk := StrTran( cTextOk, "‹", "" )
	cTextOk := StrTran( cTextOk, "Á", "" )
	cTextOk := StrTran( cTextOk, "«", "" )

Return( cTextOk )
