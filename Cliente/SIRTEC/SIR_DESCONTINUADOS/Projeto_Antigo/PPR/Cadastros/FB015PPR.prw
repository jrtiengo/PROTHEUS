#Include 'Totvs.ch'
#Include 'Rwmake.ch'

/*

Ŀ
Programa   FB015PPR  Autor  Felipe S. Raota              Data  30/04/14  
Ĵ
Unidade    TRS              Contato  felipe.raota@totvs.com.br             
Ĵ
Descricao  Cadastro de Valores PPR por Unidade.                              
                                                                             
Ĵ
Uso        Especifico para cliente Sirtec - Projeto PPR                      
Ĵ
Analista    Data   Manutencao Efetuada                                      
Ĵ
            /  /                                                            
ٱ

*/

User Function FB015PPR()

Local oDlg, oSay1, oSay2, oSay3, oSay4, oSay5, oTGet, oTGet2, oPane1, oPane2, oPane3, oTButton1, oTButton2, oTButton3
Local aColsZL  := {}
Local aYesAlt  := {}
Local aYesCmp  := {}
Local aSizeAut := MsAdvSize()

Local oFont14  := TFont():New( "Arial",,14,,.F.,,,,,.F. )
Local oFont16N := TFont():New( "Arial",,16,,.T.,,,,,.F. )
Local oFont22N := TFont():New( "Arial",,22,,.T.,,,,,.F. )

Local aTipos := {}

Local cDescTel := ""

Private oCombo := NIL

Private oGDVarM
Private aHeadZL  := {}

Private _aPosObj := {{002,002,499,030},; // TPanel 1
					 {034,002,499,226},; // TPanel 2
					 {245,002,499,014},; // TPanel 3
					 {002,002,497,209}}  // MsNewGetDados

Private cMesAno  := Space(7)
Private cTipo    := Space(1)

Private cPerg := PADR("FB015PPR", 10, " ") //PADR("FB015PPR", LEN(SX1->X1_GRUPO), " ")

ValidPerg()
If !Pergunte(cPerg,.T.)
	Return
Endif

dbSelectArea("SZL")

If MV_PAR01 == 1
	aYesAlt  := {"ZL_UNIDADE", "ZL_DESCUN", "ZL_VALOR"}
	aYesCmp  := {"ZL_UNIDADE", "ZL_DESCUN", "ZL_VALOR"}
	aTipos   := {"","BSC","Satisfao do Cliente","Faturamento","Retrabalho"}
	cTitle   := "Cadastro de Valores por Unidade"
	
	SZL->(dbSetOrder(1))
Else
	aYesAlt  := {"ZL_SETOR", "ZL_DESSET", "ZL_VALOR"}
	aYesCmp  := {"ZL_SETOR", "ZL_DESSET", "ZL_VALOR"}
	aTipos   := {"","BSC"}
	cTitle   := "Cadastro de Valores por Setores"
	
	SZL->(dbSetOrder(2))
Endif

aHeadZL := U_GeraHead("SZL",.T.,,aYesCmp,.T.)

DEFINE MSDIALOG oDlg TITLE cTitle  From 00,00 To 520,1000 OF oMainWnd PIXEL
	
	// Painel 1
	oPane1 := TPanel():New(_aPosObj[1,1],_aPosObj[1,2],"",oDlg,,.F.,.F.,,SetTransparentColor(CLR_BLUE ,080),_aPosObj[1,3],_aPosObj[1,4],.F.,.F.)
	
	oSay1  := TSay():New(003,002,{||"Ms/Ano: "  },oPane1,,oFont16N,,,,.T.,CLR_WHITE,CLR_WHITE,200,10)
	oTGet  := TGet():New(002,040,{|u| If(Pcount()>0,(cMesAno:=u,_GerCols(@aColsZL)),cMesAno) },oPane1,030,008,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,{|| .T. },.F.,.F.,,cMesAno,,,, )
	
	oSay3  := TSay():New(018,002,{||"Tipo: "  },oPane1,,oFont16N,,,,.T.,CLR_WHITE,CLR_WHITE,200,10)
	oCombo := TComboBox():New(017,040,{|u|if(PCount()>0,(cTipo:=u,_GerCols(@aColsZL)),cTipo)},aTipos,80,45,oPane1,,{|| },,,,.T.,oFont14,,,{|| "" },,,,,'cTipo')
	
	If MV_PAR01 == 1
		cDescTel := "_Valores por Unidade_"
	Else
		cDescTel := "_Valores por Setor_"
	Endif
	
	oSay5  := TSay():New(012,350,{|| cDescTel },oPane1,,oFont22N,,,,.T.,CLR_WHITE,CLR_WHITE,200,10)
	
	// Painel 2
	oPane2  := TPanel():New(_aPosObj[2,1],_aPosObj[2,2],"",oDlg,,.F.,.F.,,SetTransparentColor(CLR_BLUE ,030),_aPosObj[2,3],_aPosObj[2,4],.F.,.F.)
	
	oGDVarM := MsNewGetDados():New(_aPosObj[4,1],_aPosObj[4,2],_aPosObj[4,4],_aPosObj[4,3],GD_UPDATE+GD_INSERT+GD_DELETE,"U_015PPRLOK()",/*Tok*/,,aYesAlt,/*freeze*/,,/*fieldok*/,/*superdel*/,,oPane2,aHeadZL,aColsZL)
	oGDVarM:Disable() 
	
	// Painel 3
	oPane3 	:= TPanel():New(_aPosObj[3,1],_aPosObj[3,2],"",oDlg,,.F.,.F.,,SetTransparentColor(CLR_BLUE ,080),_aPosObj[3,3],_aPosObj[3,4],.F.,.F.)
	
	oTButton1 	:= TButton():New( 02, 010, "&Gravar", oPane3,{|| _GrvVMet(@oSay2, @oSay4, @oTGet, @oTGet2) },40,10,,,.F.,.T.,.F.,,.F.,,,.F. )
	oTButton2 	:= TButton():New( 02, 055, "&Limpar", oPane3,{|| _CleanScr(@oSay2, @oSay4, @oTGet, @oTGet2) },40,10,,,.F.,.T.,.F.,,.F.,,,.F. )
	
	oTButton3 	:= TButton():New( 02, 450, "&Fechar", oPane3,{|| oDlg:End() },40,10,,,.F.,.T.,.F.,,.F.,,,.F. ) 
	
ACTIVATE MSDIALOG oDlg CENTERED

Return

/*

Ŀ
Funo     _GerCols    Autor  Felipe S. Raota             Data  10/04/13 
Ĵ
Descrio  Gera linhas do aCols para a grid.                                 
                                                                             
Ĵ
Uso        FB006PPR                                                          
ٱ

*/

Static Function _GerCols(aC)

Local cTp := ""

If cTipo == "BSC"
	cTp := "B"
ElseIf cTipo == "Satisfao do Cliente"
	cTp := "S"
ElseIf cTipo == "Faturamento"
	cTp := "F"
ElseIf cTipo == "Retrabalho"
	cTp := "R"
Endif

If !Empty(cMesAno) .AND. !Empty(cTp)
	
	If SZL->(MsSeek( xFilial("SZL") + cMesAno + cTp ))
		
		aC := {}
		
		While SZL->(!EoF()) .AND. xFilial("SZL") + cMesAno + cTp == SZL->ZL_FILIAL + SZL->ZL_MESANO + SZL->ZL_TIPO
			
			If MV_PAR01 == 1
				
				If !Empty(SZL->ZL_UNIDADE)
					_cDescUN := fBuscaCPO("CTT", 1, xFilial("CTT") + SZL->ZL_UNIDADE, "CTT_DESC01")
					aADD(aC, {SZL->ZL_UNIDADE, _cDescUN, SZL->ZL_VALOR, .F.})
				Endif
				
			Else
				If !Empty(SZL->ZL_SETOR)
					_cDescUN := fBuscaCPO("SQB", 1, xFilial("SQB") + SZL->ZL_SETOR, "QB_DESCRIC")
					aADD(aC, {SZL->ZL_SETOR, _cDescUN, SZL->ZL_VALOR, .F.})
				Endif
			Endif
			
			SZL->(dbSkip())
		Enddo
		
		If len(aC) == 0
			aADD(aC, U_LinVazia(aHeadZL))
		Endif
		
		_GetDadHab(aC, .T.)
		
	Else
		aC := {}
		aADD(aC, U_LinVazia(aHeadZL))
		
		_GetDadHab(aC, .T.)
	Endif

Else
	aC := {}
	
	// Validao pode ser executada antes de criar o objeto.
	If TYPE("oGDVarM") == "O"
		oGDVarM:Disable()
	Endif
	
Endif

Return

/*

Ŀ
Funo     015PPRLOK   Autor  Felipe S. Raota             Data  30/04/14 
Ĵ
Descrio  Valida linha do aCols.                                            
                                                                             
Ĵ
Uso        FB015PPR                                                          
ٱ

*/

User Function 015PPRLOK() 

Local aAux := oGDVarM:aCols
Local lOk  := .T.

// Validaes

Return lOk

/*

Ŀ
Funo     _015TOK     Autor  Felipe S. Raota             Data  10/04/13 
Ĵ
Descrio  Valida aCols completo.                                            
                                                                             
Ĵ
Uso        FB015PPR                                                          
ٱ

*/

Static Function _015TOK()

Local lOk   := .T.

/*
For _y:=1 to len(aAux2)
	
	If !GdDeleted(_y, aHeadZL, aAux2)
		
		//....
		
	Endif
	
Next
*/

Return lOk

/*

Ŀ
Funo     _GrvVMet     Autor  Felipe S. Raota            Data  30/04/14 
Ĵ
Descrio  Efetua gravao dos dados.                                        
                                                                             
Ĵ
Uso        FB015PPR                                                          
ٱ

*/

Static Function _GrvVMet(oSay2, oSay4, oTGet, oTGet2)

Local cTp := ""

If cTipo == "BSC"
	cTp := "B"
ElseIf cTipo == "Satisfao do Cliente"
	cTp := "S"
ElseIf cTipo == "Faturamento"
	cTp := "F"
ElseIf cTipo == "Retrabalho"
	cTp := "R"
Endif

_aC := oGDVarM:aCols

If _015TOK()
	
	If SZL->(MsSeek( xFilial("SZL") + cMesAno + cTp ))
		
		While SZL->(!EoF()) .AND. xFilial("SZL") + cMesAno + cTp == SZL->ZL_FILIAL + SZL->ZL_MESANO + SZL->ZL_TIPO
			
			If MV_PAR01 == 1
				If Empty(SZL->ZL_UNIDADE)
					SZL->(dbSkip())
					LOOP
				Endif
			Else
				If Empty(SZL->ZL_SETOR)
					SZL->(dbSkip())
					LOOP
				Endif
			Endif
			
			RecLock("SZL", .F.)
				SZL->(dbDelete())
			MsUnLock()
			
			SZL->(dbSkip())
		Enddo
		
	Endif
	
	For _x:=1 to len(_aC)
		
		If !GdDeleted(_x, aHeadZL, _aC)
			
			RecLock("SZL", .T.)
				
				SZL->ZL_FILIAL   := xFilial("SZl")
				SZL->ZL_MESANO   := cMesAno
				SZL->ZL_TIPO     := cTp
				If MV_PAR01 == 1
					SZL->ZL_UNIDADE := GDFieldGet( "ZL_UNIDADE", _x, .F., aHeadZL, _aC )
				Else
					SZL->ZL_SETOR   := GDFieldGet( "ZL_SETOR", _x, .F., aHeadZL, _aC )
				Endif
				
				SZL->ZL_VALOR    := GDFieldGet( "ZL_VALOR",   _x, .F., aHeadZL, _aC )
				
			MsUnLock()
		Endif 
		
	Next
	
	// Mes/Ano
	cMesAno := Space(7)
	oTGet:SetText(cMesAno)
	
	// Tipo
	cTipo := Space(1)
	//oTGet2:SetText(cTipo)
	oCombo:Select(1)
	
	aColsZL := {}
	aADD(aColsZL, U_LinVazia(aHeadZL))
	_GetDadHab(aColsZL, .F.)
	
	If MV_PAR01 == 1
		MsgInfo("Valores por unidade, gravados com sucesso.")
	Else
		MsgInfo("Valores por setor, gravados com sucesso.")
	Endif
	
Endif

Return

/*

Ŀ
Funo     _CleanScr   Autor  Felipe S. Raota             Data  10/04/13 
Ĵ
Descrio  Efetua gravao dos dados.                                        
                                                                             
Ĵ
Uso        FB006PPR                                                          
ٱ

*/

Static Function _CleanScr(oSay2, oSay4, oTGet, oTGet2)

// Mes/Ano
cMesAno := Space(7)
oTGet:SetText(cMesAno)

// Tipo
cTipo := Space(1)
//oTGet2:SetText(cTipo)
oCombo:Select(1)

aColsZL := {}
aADD(aColsZL, U_LinVazia(aHeadZL))
_GetDadHab(aColsZL, .F.)

Return

/*

Ŀ
Funo     _GetDadHab  Autor  Felipe S. Raota             Data  10/04/13 
Ĵ
Descrio  Habilita ou Desabilita GetDados                                   
                                                                             
Ĵ
Uso        FB006PPR                                                          
ٱ

*/

Static Function _GetDadHab(aC, lHab)

aColsZL := aC
oGDVarM:aCols := aColsZL
oGDVarM:ForceRefresh()

If lHab
	oGDVarM:Enable()
Else
	oGDVarM:Disable()
Endif

Return

/*

Ŀ
Funo     ValidPerg   Autor  Felipe S. Raota             Data  24/04/13 
Ĵ
Descrio  Grupo de Perguntas.                                               
                                                                             
Ĵ
Uso        FB007PPR                                                          
ٱ

*/

Static Function ValidPerg()

local _aArea  := GetArea ()
local _aRegs  := {}
local _aHelps := {}
local _i      := 0
local _j      := 0

_aRegs = {}
//           GRUPO  ORDEM PERGUNT                           PERSPA PERENG VARIAVL   TIPO TAM DEC PRESEL GSC  VALID         VAR01       DEF01     				DEFSPA1             DEFENG1             CNT01 VAR02     DEF02             	DEFSPA2             DEFENG2            CNT02 VAR03 DEF03   		DEFSPA3  DEFENG3   CNT03 VAR04 DEF04 DEFSPA4  DEFENG4   CNT04 VAR05 DEF05   DEFSPA5  DEFENG5  CNT05      F3    GRPSXG
aADD(_aRegs,{cPerg, "01", "Tipo de separao          ?  ", "",    "",    "mv_ch1", "C", 01, 0,  0,     "C", "",      "mv_par01",      "Por Unidade        ",	"",                 "",                 "",   "",   		"Por Setor",         "",                 "",                "",   "",   "",     		"",      "",      "",   "",   "",    "",      "",       "",   "",   "",     "",      "",      "",        "",     ""})

// Definicao de textos de help (versao 7.10 em diante): uma array para cada linha.
_aHelps = {}    //               1         2         3         4             1         2         3         4             1         2         3         4
//              Ordem   1234567890123456789012345678901234567890    1234567890123456789012345678901234567890    1234567890123456789012345678901234567890
//AADD (_aHelps, {"01", {"Informar Data Inicial         ","",""}})

/*
DbSelectArea ("SX1")
DbSetOrder (1)
For _i := 1 to Len (_aRegs)
	If ! DbSeek (cPerg + _aRegs [_i, 2])
		RecLock("SX1", .T.)
	Else
		RecLock("SX1", .F.)
	Endif
	For _j := 1 to FCount ()
		// Campos CNT nao sao gravados para preservar conteudo anterior.
		If _j <= Len (_aRegs [_i]) .and. left (FieldName (_j), 6) != "X1_CNT" .and. FieldName (_j) != "X1_PRESEL"
			FieldPut(_j, _aRegs [_i, _j])
		Endif
	Next
	MsUnlock()
Next

// Deleta do SX1 as perguntas que nao constam em _aRegs
DbSeek (cPerg, .T.)
do while ! eof () .and. x1_grupo == cPerg
	if ascan (_aRegs, {|_aVal| _aVal [2] == sx1 -> x1_ordem}) == 0
		reclock("SX1", .F.)
		dbdelete()
		msunlock()
	endif
	dbskip()
enddo

// Gera helps das perguntas
For _i := 1 to Len (_aHelps)
	PutSX1Help ("P." + AllTrim(cPerg) + _aHelps [_i, 1] + ".", _aHelps [_i, 2], {}, {})
Next
*/

Restarea(_aArea)

Return