#include "protheus.ch"
#INCLUDE "rwmake.ch"
#INCLUDE "tbiconn.ch"

/*/{Protheus.doc} DEVUPD0

Tela para sele็ใo da empresa

@author  Fernando Alencar
@version P11 e P10
@since   13/11/2011
@return
@obs

/*/
User Function DEVUPD0()

	Local aData 	 	:= {}
	Local aSelData 	:= {}

	aData 	 	:= _LoadData()
	aSelData 	:= _SelectData(aData)

Return aSelData

/*/{Protheus.doc} _LoadData

Carrega a lista de empresas do ambiente

@author  Fernando Alencar
@version P11 e P10
@since   15/09/2011
@return  empresas selecionadas
@obs

/*/
Static Function _LoadData()

	Local aData := {}

	dbSelectArea( 'SM0' )
	dbGoTop()

	While !SM0->( EOF() )
		aAdd( aData, { Recno(), SM0->M0_CODIGO, SM0->M0_CODFIL, SM0->M0_FILIAL } )
		SM0->( dbSkip() )
	End

Return aData


/*/{Protheus.doc} SelectTools

Exibe tela onde o usuแrio poderแ selecionar quais empresas

@author  Fernando Alencar
@version P11 e P10
@since   15/09/2011
@return  empresas selecionadas
@obs

/*/
Static Function _SelectData(aData)

	Local lInverte 	:= .f.
	Local cMarca 		:= "OK"
	Local aHeader 		:= _Header()
	Local cAliasTRB 	:= _CriaTrab(aData)
	Local oMainWnd
	Local aData		:= {}
	Local nOpc 		:= 0

	BLACK_COLOR 	:= 0
	BLUE_COLOR 	:= 8388608
	WHITE_COLOR 	:= 16777215
	GRAY_COLOR 	:= 8421504

	oMainWnd 	:= MsDialog():New(0,0,400,500, "DevTools",,,,,,,,,.T.)

	oFont:= TFont():New('Arial',,60,.t.)
	oPanel:= tPanel():New(0,0,"DICIONมRIO",oMainWnd,oFont,.F.,,WHITE_COLOR,GRAY_COLOR,255,22,,)

	oBrwTrb := MsSelect():New(cAliasTRB,"OK","",aHeader,@lInverte,@cMarca,{30,5,175,245})
	Eval(oBrwTrb:oBrowse:bGoTop)
	oBrwTrb:oBrowse:Refresh()

	TButton():New(180,185,'Executar'	,oMainWnd,{|| nOpc := 1, oMainWnd:End() },60,15,,,,.T.)

	oMainWnd:Activate(,,,.T.)

	If nOpc = 1
		aData := _GetData(cAliasTRB, cMarca)
	EndIf

	DbCloseArea(cAliasTRB)

Return aData

/*/{Protheus.doc} Header

@author  Fernando Alencar
@since   13/11/2011
@return  Array

/*/
Static Function _Header()

	Local aHeader := {}

	aadd(aHeader,{"OK"    	,,""        			 	})
	aadd(aHeader,{"codigo" 	,,"Empresa"    			})
	aadd(aHeader,{"Filial" 	,,"Filial"		 			})
	aadd(aHeader,{"Nome" 		,,"Nome"		 			})

Return aHeader

/*/{Protheus.doc} _CriaTrab

@author  Fernando Alencar
@since   13/11/2011
@return  Alias

/*/
Static Function _CriaTrab(aData)

	Local cAliasTRB := "TRB"
	Local _aStruTrb := {}
	Local nDifere 	 := 0
	Local i			 := 0

	aadd(_aStruTrb,{"OK" 		 	,"C"	,02							,0})
	aadd(_aStruTrb,{"CODIGO" 	,"C"	,Len(aData[1][2])			,0})
	aadd(_aStruTrb,{"FILIAL"	 	,"C"	,Len(aData[1][3])			,0})
	aadd(_aStruTrb,{"NOME"	 	,"C"	,Len(aData[1][4])			,0})

	If Select(cAliasTRB) > 0
		(cAliasTRB)->(DbCloseArea())
	Endif

	_cArqEmp := CriaTrab(_aStruTrb)
	dbUseArea(.T.,__LocalDriver,_cArqEmp,cAliasTRB)

	For i := 1 To Len(aData)

		RecLock(cAliasTRB,.T.)
		TRB->OK     	:= space(2)
		TRB->CODIGO	:= aData[i][2]
		TRB->FILIAL	:= aData[i][3]
		TRB->NOME		:= aData[i][4]
		MsUnlock()

	Next

Return cAliasTRB


/*/{Protheus.doc} _GetData

Retorna as linhas selecionadas em array multidimencional

@author  Fernando Alencar
@since   13/11/2011
@return  Alias

/*/
Static Function _GetData(cAliasTRB, cMarca)

	Local aArea := (cAliasTRB)->(GetArea())
	Local aData := {}

	(cAliasTRB)->(DbGoTop())
	While (cAliasTRB)->(!EOF())
		If (cAliasTRB)->(OK) = cMarca
			aadd(aData, {(cAliasTRB)->(CODIGO)  ,;
				          (cAliasTRB)->(FILIAL) 	,;
				          (cAliasTRB)->(NOME) 	;
				         })
		EndIf

		(cAliasTRB)->(DbSkip())
	EndDo

	RestArea(aArea)

Return aData

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Programa ณ MyOpenSM บ Autor ณ Microsiga          บ Data ณ  14/06/10   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ Descricaoณ Funcao de processamento abertura do SM0 modo exclusivo     ณฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑณ Uso      ณ MyOpenSM - Gerado por EXPORDIC / Upd. V.3.10 EFS           ณฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออ?ฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function MyOpenSM0Ex()

	Local lOpen := .F.
	Local nLoop := 0

	For nLoop := 1 To 20
		dbUseArea( .T., , 'SIGAMAT.EMP', 'SM0', .F., .F. )

		If !Empty( Select( 'SM0' ) )
			lOpen := .T.
			dbSetIndex( 'SIGAMAT.IND' )
			Exit
		EndIf
		Sleep( 500 )
	Next nLoop

	If !lOpen
		ApMsgStop( 'Nใo foi possํvel a abertura da tabela ' + ;
			'de empresas de forma exclusiva.', 'ATENวรO' )
	EndIf

Return lOpen
