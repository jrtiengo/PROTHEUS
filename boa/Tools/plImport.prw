#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'PARMTYPE.CH'
#INCLUDE 'TRYEXCEPTION.CH'

#DEFINE ENTER CHR(13) + CHR(10)
#DEFINE CTIT "Gerar Planilha de Importacao"


/*/{Protheus.doc} PLIMPORT
FunÁ„o para gerar Planilha de ImportaÁ„o de dados da Tabela informada no par‚metro. 
Ser„o gerado uma planilha com os campos ObrigatÛrios onde Ser„o lido o dicion·rio de dados.
@author Edson Hornberger 
@since 22/08/2018
@version 1.0	
@type function
/*/

USER FUNCTION PLIMPORT(CTABELA)

LOCAL OEXCEPTION
LOCAL CMSGERROR	:= ""
LOCAL CPATH		:= GETTEMPPATH(.T.)
LOCAL CNAME		:= DTOS(DDATABASE) + "_" + LEFT(STRTRAN(TIME(), ":", ""), 4) + ".XML"
LOCAL OAPPEXC	:= NIL
LOCAL OEXCEL	:= FWMSEXCELEX():NEW()
LOCAL CWORKSHEET:= "Dados para Importacao"
LOCAL CTABLE 	:= ""
LOCAL AAUXNAME	:= {}
LOCAL AAUXTYPE	:= {}
LOCAL CTIPO		:= ""
LOCAL AAUXSIZE	:= {}
LOCAL AAUXDEC	:= {}
LOCAL AAUXOBR	:= {}
LOCAL AITEM		:= {}
LOCAL NI		:= 0 
LOCAL AAREA		:= GETAREA()
LOCAL AAREASX2	:= SX2->(GETAREA())
LOCAL AAREASX3	:= SX3->(GETAREA())
LOCAL LTABELA	:= .F. 

PRIVATE APERG	:= {}
PRIVATE ARESP	:= {"Informe a Tabela para gerar a Planilha!", SPACE(003)}

DEFAULT CTABELA := ""

LTABELA := !EMPTY(ALLTRIM(CTABELA))

TRYEXCEPTION
	
	IF !LTABELA
	
		AADD(APERG,{9, "Informe a Tabela para gerar a Planilha!" , 150, 040, .T.})
		AADD(APERG,{1, "Tabela SX3: "	, ARESP[02], "@!", ".T.", "SX2PAD", ".T.", 080, .T.})
		
		IF !PARAMBOX(APERG, CTIT, @ARESP,,, .T.,,,,, .T., .T.)
	
			AVISO(CTIT,"Opera√ß√£o Cancelada pelo Usu√°rio!",{"Ok"},2,"ATEN√á√ÉO")
			RETURN
		
		ENDIF
		
	ELSE
	
		ARESP[02] := CTABELA
		
	ENDIF
	
	DBSELECTAREA("SX2")
	DBSETORDER(1)
	DBSEEK(ARESP[02])
	
	CTABLE := "Tabela " + ARESP[02] + " - " + ALLTRIM(X2NOME())
	DBSELECTAREA("SX3")
	DBSETORDER(1)
	DBSEEK(ARESP[02])
	
	OEXCEL:ADDWORKSHEET(CWORKSHEET)
	OEXCEL:ADDTABLE(CWORKSHEET, CTABLE)
	
	OEXCEL:SETTITLEFONT("CAMBRIA")
	OEXCEL:SETTITLESIZEFONT(14)
	OEXCEL:SETTITLEBOLD(.T.)
	OEXCEL:SETTITLEFRCOLOR("#4169E1")
	
	//AddColumn(< cWorkSheet >, < cTable >, < cColumn >,< nAlign >(Alinhamento da coluna ( 1-Left,2-Center,3-Right )), 
	//< nFormat >(Codigo de formata√ß√£o ( 1-General,2-Number,3-Monet√°rio,4-DateTime )), < lTotal >)
	OEXCEL:ADDCOLUMN(CWORKSHEET, CTABLE, "DADOS TECNICOS"					, 1, 1)
	
	AADD(AAUXNAME	,"Descricao")
	AADD(AAUXTYPE	,"Tipo do Campo")
	AADD(AAUXSIZE	,"Tamanho do Campo")
	AADD(AAUXDEC	,"Decimal do Campo") 
	AADD(AAUXOBR	,"Campo Obrigatorio?")
	
	WHILE SX3->(!EOF()) .AND. ALLTRIM(SX3->X3_ARQUIVO) = ALLTRIM(ARESP[02])
		
		IF X3OBRIGAT(ALLTRIM(SX3->X3_CAMPO)) .OR. X3USO(SX3->X3_USADO) .OR. "NAOVAZIO" $ UPPER(ALLTRIM(SX3->X3_VALID))  
		
			OEXCEL:ADDCOLUMN(CWORKSHEET, CTABLE, ALLTRIM(SX3->X3_CAMPO)				, 2, 1)
			
			DO CASE
				
				CASE SX3->X3_TIPO = "C"
					CTIPO := "Caracter"
				
				CASE SX3->X3_TIPO = "N"
					CTIPO := "Numerico"
					
				CASE SX3->X3_TIPO = "L"
					CTIPO := "Logico"
				
				CASE SX3->X3_TIPO = "D"
					CTIPO := "Data"
					
				CASE SX3->X3_TIPO = "M"
				 	CTIPO := "Memo"
				
			ENDCASE
			
			AADD(AAUXNAME	, X3TITULO())
			AADD(AAUXTYPE	, CTIPO)
			AADD(AAUXSIZE	, STRZERO(SX3->X3_TAMANHO, 3))
			AADD(AAUXDEC	, STRZERO(SX3->X3_DECIMAL, 1))
			AADD(AAUXOBR	, IIF(X3OBRIGAT(ALLTRIM(SX3->X3_CAMPO)), "Sim", "Nao"))
			
		ENDIF
		
		SX3->(DBSKIP())
		
	ENDDO
	
	OEXCEL:SETHEADERFONT("CAMBRIA")
	OEXCEL:SETHEADERSIZEFONT(10)
	OEXCEL:SETHEADERBOLD(.T.)
	OEXCEL:SETFRCOLORHEADER("#BCD8E9")
	OEXCEL:SETBGCOLORHEADER("#2D519A")
	
	OEXCEL:SETCELFRCOLOR("#FFFFFF")
	OEXCEL:SETCELFONT("CAMBRIA")
	OEXCEL:SETCELSIZEFONT(8)
	OEXCEL:SETCELBGCOLOR("#0082c8")
	
	AITEM := {}
	FOR NI := 1 TO LEN(AAUXNAME)
		
		AADD(AITEM, NI)
		
	NEXT NI 
	
	OEXCEL:ADDROW(CWORKSHEET, CTABLE, AAUXNAME, AITEM)
	
	AITEM := {}
	FOR NI := 1 TO LEN(AAUXTYPE)
		
		AADD(AITEM, NI)
		
	NEXT NI 
	
	OEXCEL:ADDROW(CWORKSHEET, CTABLE, AAUXTYPE, AITEM)
	
	AITEM := {}
	FOR NI := 1 TO LEN(AAUXSIZE)
		
		AADD(AITEM, NI)
		
	NEXT NI 
	
	OEXCEL:ADDROW(CWORKSHEET, CTABLE, AAUXSIZE, AITEM)
	
	AITEM := {}
	FOR NI := 1 TO LEN(AAUXDEC)
		
		AADD(AITEM, NI)
		
	NEXT NI 
	
	OEXCEL:ADDROW(CWORKSHEET, CTABLE, AAUXDEC, AITEM)
	
	AITEM := {}
	FOR NI := 1 TO LEN(AAUXOBR)
		
		AADD(AITEM, NI)
		
	NEXT NI 
	
	OEXCEL:ADDROW(CWORKSHEET, CTABLE, AAUXOBR, AITEM)
	
	IF RIGHT(ALLTRIM(CPATH),1) = "\"
		CPATH := ALLTRIM(CPATH) + CNAME 
	ELSE
		CPATH := ALLTRIM(CPATH) + "\" + CNAME
	ENDIF
	
	OEXCEL:ACTIVATE()
	OEXCEL:GETXMLFILE(CPATH)
	
	OAPPEXC := MSEXCEL():NEW() 
	OAPPEXC:WORKBOOKS:OPEN( CPATH )  
	OAPPEXC:SETVISIBLE(.T.)
	OAPPEXC:DESTROY()
	
	OEXCEL:DEACTIVATE()
	OEXCEL := FREEOBJ( OEXCEL )
	
CATCHEXCEPTION USING OEXCEPTION 

	IF ( VALTYPE( OEXCEPTION ) == "O" )

		CMSGERROR += CAPTUREERROR()
		AVISO("ERROR CAPTURED",CMSGERROR,{"OK"},3,"ERROR",,"ENGRENAGEM")
		__QUIT()

	ENDIF	

ENDEXCEPTION NODELSTACKERROR

RESTAREA(AAREASX3)
RESTAREA(AAREASX2)
RESTAREA(AAREA)
	
RETURN
