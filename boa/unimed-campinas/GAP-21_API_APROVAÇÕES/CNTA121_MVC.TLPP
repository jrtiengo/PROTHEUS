#Include 'TOTVS.ch'
#Include 'TopConn.ch'
#Include "TBIConn.ch"
#Include 'Protheus.ch'
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#INCLUDE "FWMVCDEF.CH"

#Define cTitApp "Ponto de Entrada CNTA121"

/*/{Protheus.doc} CNTA121
Ponto de Entrada MVC CNTA121
@type function
@version 1.0 
@author Tiengo Jr.
@since 05/05/2025
@Obs PARAMIXB[1] O - MODELCOMMITNTTS - Objeto do formulário ou do modelo, conforme o caso
@Obs PARAMIXB[2] C - MODELCOMMITNTTS - ID do local de execução do ponto de entrada
@Obs PARAMIXB[3] C - MODELCOMMITNTTS - ID do formulário
@Obs PARAMIXB[4] L - FORMCOMMITTTSPOS - Se .T. indica novo registro (Inclusão) se .F. registro já existente (Alteração / Exclusão)
/*/

User Function CNTA121()

	Local aArea         := FwGetArea()              as Array
	Local aAreaCND      := CND->(FwGetArea())       as Array
	Local aAreaCXN      := CXN->(FwGetArea())       as Array
	Local aParam  		:= PARAMIXB                 as Array
	Local xRet    		:= .T.                      as Logical
	Local oObj    		:= Nil                      as Object
	Local cIdPonto		:= ''                       as Character
	Local cIdModel		:= ''                       as Character
	Local cTipo         := 'MED'                    as Character
	Local nOpc          := 0                        as Numeric
	Local oRateio		:= Nil						as Object
	Local nI			:= 0						as Numeric
	Local nZ			:= 0						as Numeric

	TRY

		If aParam <> NIL

			oObj       := aParam[1]
			cIdPonto   := aParam[2]
			cIdModel   := aParam[3]

			nOpc       := oObj:GetOperation()

			If cIdPonto == 'MODELCOMMITNTTS' //Após a gravação total do modelo e fora da transação.

				//GAP 021 - Verifica se o contrato está aguardando aprovação e envia ao Fluig
				If ! Empty(CND->CND_APROV)
					//U_IntAprovFluig(nOpc, cTipo, CND->CND_NUMMED, '', CND->CND_XIDFLU)
				EndIf

				//GAP 201 - Se o CND_XRATEI tiver dados, ira tratar para gravar na tabela SZ0 os rateios
				If ! Empty(CND->CND_XRATEI) .and. nOpc == 4 .and. FWIsInCallStack('CN121MedEnc')

					oRateio	:= JSONObject():New()
					oRateio:FromJSON(CND->CND_XRATEI)

					DBSelectArea("SZ0")
					SZ0->(dbSetOrder(1)) //Z0_FILIAL+Z0_NUMPED+Z0_ITEM

					For nI := 1 to Len(oRateio['contabil'])
						RecLock("SZ0",.T.)
						SZ0->Z0_FILIAL  := FWxFilial("SZ0")
						SZ0->Z0_NUMPED  := SC7->C7_NUM
						SZ0->Z0_IDINT   := CND->CND_XIDINT
						SZ0->Z0_FORNECE := SC7->C7_FORNECE
						SZ0->Z0_LOJA    := SC7->C7_LOJA
						For nZ := 1 to len(oRateio['contabil'][nI]:Getnames())
							If Alltrim(oRateio['contabil'][nI]:Getnames()[nZ]) == "Z0_EMISSAO"
								SZ0->&(oRateio['contabil'][nI]:Getnames()[nZ]) := SC7->C7_EMISSAO
							Else
								SZ0->&(oRateio['contabil'][nI]:Getnames()[nZ]) := oRateio['contabil'][nI][oRateio['contabil'][nI]:GetNames()[nZ]]
							Endif
						Next nZ
						SZ0->(MsUnLock())
					Next nI
				EndIf
			Endif
		Endif

	CATCH oError

		U_AdminMsg("[CNTA121] " + DToC(Date()) + " - " + Time() + " -> " + oError:Description, IsBlind(), oError)

	ENDTRY

	FwRestArea(aArea)
	FwRestArea(aAreaCND)
	FwRestArea(aAreaCXN)

Return(xRet)
