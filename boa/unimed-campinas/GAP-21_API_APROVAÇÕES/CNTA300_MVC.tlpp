#Include 'TOTVS.ch'
#Include 'TopConn.ch'
#Include 'Protheus.ch'
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#INCLUDE "FWMVCDEF.CH"

#Define cTitApp "Ponto de Entrada CNTA300"

/*/{Protheus.doc} CNTA300
Ponto de Entrada MVC CNTA300
@type function
@version 1.0 
@author Tiengo Jr.
@since 05/05/2025
@Obs PARAMIXB[1] O - FORMCOMMITTTSPOS - Objeto do formulário ou do modelo, conforme o caso
@Obs PARAMIXB[2] C - FORMCOMMITTTSPOS - ID do local de execução do ponto de entrada
@Obs PARAMIXB[3] C - FORMCOMMITTTSPOS - ID do formulário
@Obs PARAMIXB[4] L - FORMCOMMITTTSPOS - Se .T. indica novo registro (Inclusão) se .F. registro já existente (Alteração / Exclusão)
/*/

User Function CNTA300()

	Local aArea         := FwGetArea()              as Array
	Local aAreaCN9      := CN9->(FwGetArea())       as Array
	Local aAreaCNC      := CNC->(FwGetArea())       as Array
	Local aParam  		:= PARAMIXB					as Array
	Local xRet    		:= .T.						as Logical
	Local oObj    		:= Nil						as Object
	Local cIdPonto		:= ''						as Character
	Local cIdModel		:= ''						as Character
	Local lOperacao		:= .F.						as Logical
	Local cTipo         := 'CTR'					as Character
	Local nOpc          := 0                        as Numeric

	TRY

		If aParam <> NIL

			oObj       := aParam[1]
			cIdPonto   := aParam[2]
			cIdModel   := aParam[3]

			nOpc       := oObj:GetOperation()

			If cIdPonto == 'FORMCOMMITTTSPOS' //Após a gravação da tabela do formulário.

				lOperacao := aParam[4]

				//GAP112 - Se o contrato tiver origem do novo fluxo de compras, irá nascer como 2N = Em Elaboração Area Negócio.
				If FWIsInCallStack('PGCA010')
					If lOperacao .and. cIdModel == 'CN9MASTER'
						Reclock("CN9", .F.)
						CN9->CN9_XSITUA := '2N'
						CN9->(msUnlock())
					Endif
				Else
					Reclock("CN9", .F.)
					CN9->CN9_XSITUA := CN9->CN9_SITUAC
					CN9->(msUnlock())
				Endif

			Elseif cIdPonto == 'MODELCOMMITNTTS' //Após a gravação total do modelo e fora da transação.
				//GAP - 021 Verifica se o contrato está aguardando aprovação e envia ao Fluig
				If ! Empty(CN9->CN9_APROV) .and. (CN9->CN9_SITUAC == '04' .or. CN9->CN9_SITUAC == '09' )

					U_IntAprovFluig(nOpc, cTipo, CN9->CN9_NUMERO, CN9->CN9_REVISA, CN9->CN9_XIDFLU)
				EndIf
			EndIf
		Endif

	CATCH oError

		U_AdminMsg("[CNTA121] " + DToC(Date()) + " - " + Time() + " -> " + oError:Description, IsBlind(), oError)

	ENDTRY

	FwRestArea(aArea)
	FwRestArea(aAreaCN9)
	FwRestArea(aAreaCNC)	

Return(xRet)
