#Include 'TOTVS.ch'
#Include 'TopConn.ch'
#Include 'Protheus.ch'
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'

#Define cTitApp "Ponto de Entrada M110STTS"

/*/{Protheus.doc} M110STTS
Inclusão de interface após gravar a solicitação
1. GAP 068 - Envio de Aprovações Fluig Compras
@type function
@version V 1.00
@author Tiengo Junior
@since 30/06/2025
@Param PARAMIXB[1] - C - Numero da Solicitação
@Param PARAMIXB[2] - N - Operação: 1 = Inclusão, 2 = Alteração, 3 = Exclusão
@Param PARAMIXB[3] - L - Se a Solicitação de Compra é originada de uma cópia (tipo: LÓGICO)
@see https://tdn.totvs.com/pages/releaseview.action?pageId=6085312
/*/
User Function M110STTS()

	Local aArea         := FwGetArea()            as Array
	Local aAreaSC1      := SC1->(FwGetArea())     as Array
	Local oError        := ErrorClass():New()     as Object
	Local cNumSc        := PARAMIXB[1]            as Character

	TRY
		//Envia Cancelamento ao Fluig
		If ! Empty(SC1->C1_XIDFLU)
			U_IntCancFluig('SC1', Alltrim(SC1->C1_XIDFLU), SC1->(RecNo()))
		Endif

		//Envia Documento ao Fluig
		If SCR->(MSSeek(FWxFilial("SCR") + "SC" + cNumSc)) //CR_FILIAL+CR_TIPO+CR_NUM+CR_NIVEL
			U_IntAprovFluig('SC1', cNumSc, '', '')
		Endif

	CATCH oError

		U_AdminMsg("[M110STTS] " + DToC(Date()) + " - " + Time() + " -> " + oError:Description, IsBlind(), oError)

	ENDTRY

	FwRestArea(aArea)
	FwRestArea(aAreaSC1)

Return()
